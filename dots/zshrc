#######################################
# ZSH
#######################################

# shortcut to this dotfiles path is $ZSH
ZSH=$HOME/.dotfiles; export ZSH

# Cache brew prefix
# Calling in a subshel each time is very slow.
BREW_PREFIX=$(brew --prefix); export BREW_PREFIX

# Use 256 colours
TERM=xterm-256color

# Helps with builds
CONFIGURE_ARGS="--with-cflags='$CFLAGS' --with-ldflags='$LDFLAGS'"; export CONFIGURE_ARGS

# Ask before executing rm with a star rm folder/*.
# https://github.com/sindresorhus/guides/blob/master/how-not-to-rm-yourself.md
unsetopt RM_STAR_SILENT

# Always use vim as the visual editor
VISUAL=vim; export VISUAL

#######################################
# Prompt
#######################################

autoload -U promptinit && promptinit
prompt pure

#######################################
# GRC
#######################################

# GRC colorizes unix tools
if (( $+commands[grc] )); then
  source "$BREW_PREFIX/etc/grc.bashrc"
fi

#######################################
# Path
#######################################

# Add homebrew bins to path
PATH=$BREW_PREFIX/bin:$BREW_PREFIX/sbin:$PATH;

# Set GEM_HOME for Ruby gems
export GEM_HOME="$HOME/.gems"

# Add custom gem home to path
PATH=$PATH:$GEM_HOME/bin;

# Add coreutiles to PATH
PATH=$PATH:$BREW_PREFIX/opt/coreutils/libexec/gnubin;

# Add dotfile binaries to PATH
PATH="$ZSH/bin:$PATH";

# Add python binaries to path
if hash python3 2>/dev/null; then
  PATH="$HOME/Library/Python/3.6/bin:$PATH";
fi

# Add go binaries to path
# https://ahmadawais.com/install-go-lang-on-macos-with-homebrew/
if hash go 2>/dev/null; then
  export GOPATH="${HOME}/.go"
  export GOROOT="$(brew --prefix golang)/libexec"
  PATH="${GOPATH}/bin:${GOROOT}/bin:$PATH"
fi

export PATH;

#######################################
# Topics
#######################################

function() {
  # all of our zsh files
  typeset -U config_files
  local config_files=($HOME/.dotfiles/topics/**/*.zsh)

  # Load everything but the completion files
  for file in ${config_files:#*/completion.zsh}; do
    source $file
  done

  # Completions
  fpath=($BREW_PREFIX/share/zsh-completions $fpath)

  # Initialize autocompletion
  autoload -U compinit
  compinit

  # Load every completion after autocomplete loads
  for file in ${(M)config_files:#*/completion.zsh}; do
    source $file
  done
}

#######################################
# Autojump
#######################################

function () {
  [ -f /usr/local/etc/profile.d/autojump.sh ] && . /usr/local/etc/profile.d/autojump.sh
}

#######################################
# Sources
#######################################

function () {
  # iTerm 2
  test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

  # brew install zsh-syntax-highlighting
  test -e "/usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh" && source /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

  # NVM
  export NVM_DIR="$HOME/.nvm"
  [ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"
}
