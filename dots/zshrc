# Set up a shortcut to this dotfiles path
ZSH=$HOME/.dotfiles; export ZSH

# Use 256 colours
TERM=xterm-256color

# Ask before executing rm with a star rm folder/*.
# https://github.com/sindresorhus/guides/blob/master/how-not-to-rm-yourself.md
unsetopt RM_STAR_SILENT

# History file configuration
if test -z "$HISTFILE"; then
  HISTFILE="$HOME/.zsh_history"
fi

HISTSIZE=50000
SAVEHIST=10000

# History command configuration
setopt extended_history # record timestamp of command in HISTFILE
setopt hist_expire_dups_first # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_ignore_dups # ignore duplicated commands history list
setopt hist_ignore_space # ignore commands that start with space
setopt hist_verify # show command with history expansion to user before running it
setopt inc_append_history # add commands to HISTFILE in order of execution
setopt share_history # share command history data

# Conditionally set up homebrew config (macOS only)
if which brew > /dev/null 2>&1; then
  # site-functions required for pure when using homebrew
  fpath+=($HOMEBREW_PREFIX/share/zsh/site-functions)
  fpath+=($HOMEBREW_PREFIX/share/zsh-completions)
fi

# Check if we've installed pure manually
if test -d $HOME/.zsh/pure; then
  fpath+=($HOME/.zsh/pure)
fi

# Initialise the pure prompt
autoload -U promptinit && promptinit
prompt pure

# Initialize autocompletion
autoload -U compinit
compinit

# matches case insensitive for lowercase
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# pasting with tabs doesn't perform completion
zstyle ':completion:*' insert-tab pending

# Add dotfile binaries to our $PATH
PATH="$ZSH/bin:$PATH";

# all of our zsh files
typeset -U config_files
local config_files=($ZSH/topics/**/*.zsh)

# Source all topic config files
for file in $config_files; do
  source $file
done

# Export any $PATH modifications
export PATH;
