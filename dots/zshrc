#######################################
# ZSH
#######################################

# shortcut to this dotfiles path is $ZSH
ZSH=$HOME/.dotfiles; export ZSH

# Cache brew prefix
# Calling in a subshel each time is very slow.
BREW_PREFIX=$(brew --prefix); export BREW_PREFIX

# Use 256 colours
TERM=xterm-256color

# Helps with builds
CONFIGURE_ARGS="--with-cflags='$CFLAGS' --with-ldflags='$LDFLAGS'"; export CONFIGURE_ARGS

# Ask before executing rm with a star rm folder/*.
# https://github.com/sindresorhus/guides/blob/master/how-not-to-rm-yourself.md
unsetopt RM_STAR_SILENT

# Always use vim as the visual editor
VISUAL=vim; export VISUAL

#######################################
# Prompt
#######################################

autoload -U promptinit && promptinit
prompt pure

#######################################
# GRC
#######################################

# GRC colorizes unix tools
if (( $+commands[grc] )); then
  source "$BREW_PREFIX/etc/grc.zsh"
fi

#######################################
# Path
#######################################

# Add homebrew bins to path
PATH=$BREW_PREFIX/bin:$BREW_PREFIX/sbin:$PATH;

# Add coreutiles to PATH
PATH=$PATH:$BREW_PREFIX/opt/coreutils/libexec/gnubin;

# Add dotfile binaries to PATH
PATH="$ZSH/bin:$PATH";

export PATH;

#######################################
# Topics
#######################################

function() {
  # all of our zsh files
  typeset -U config_files
  local config_files=($HOME/.dotfiles/topics/**/*.zsh)

  # Load everything but the completion files
  for file in ${config_files:#*/completion.zsh}; do
    source $file
  done

  # Completions
  fpath=($BREW_PREFIX/share/zsh-completions $fpath)

  # Initialize autocompletion
  autoload -U compinit
  compinit

  # Load every completion after autocomplete loads
  for file in ${(M)config_files:#*/completion.zsh}; do
    source $file
  done
}

#######################################
# Sources
#######################################

function () {
  # Autojump
  local autojump_path=/usr/local/etc/profile.d/autojump.sh
  [ -f $autojump_path ] && . $autojump_path

  # brew install zsh-syntax-highlighting
  local zsh_syntax_highlighting_path=/usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
  [ -f $zsh_syntax_highlighting_path ] && . $zsh_syntax_highlighting_path

  # NVM
  export NVM_DIR="$HOME/.nvm"
  [ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"

  # rbenv
  if which rbenv > /dev/null; then
    eval "$(rbenv init -)";
  fi
}
