#######################################
# ZSH
#######################################

# shortcut to this dotfiles path is $ZSH
ZSH=$HOME/.dotfiles; export ZSH

# Cache brew prefix
# Calling in a subshel each time is very slow.
BREW_PREFIX=$(brew --prefix); export BREW_PREFIX

# Use 256 colours
TERM=xterm-256color

# Helps with builds
CONFIGURE_ARGS="--with-cflags='$CFLAGS' --with-ldflags='$LDFLAGS'"; export CONFIGURE_ARGS

# Ask before executing rm with a star rm folder/*.
# https://github.com/sindresorhus/guides/blob/master/how-not-to-rm-yourself.md
unsetopt RM_STAR_SILENT

# Always use vim as the visual editor
VISUAL=vim; export VISUAL

#######################################
# Prompt
#######################################

autoload -U promptinit && promptinit
prompt pure

#######################################
# GRC
#######################################

# GRC colorizes unix tools
if (( $+commands[grc] )); then
  source "$BREW_PREFIX/etc/grc.bashrc"
fi

#######################################
# Path
#######################################

# Make sure /usr/local (Homebrew) comes first
PATH=$BREW_PREFIX/bin:$BREW_PREFIX/sbin:$PATH; export PATH

# NPM & rbenv enhanced PATH
PATH=$BREW_PREFIX/share/npm/bin:$HOME/.rbenv/bin:$PATH; export PATH

# Add coreutiles to PATH
PATH=$BREW_PREFIX/opt/coreutils/libexec/gnubin:$PATH; export PATH

# Add dotfile binaries to PATH
PATH="$ZSH/bin:$PATH"; export PATH

# For node, so modules are picked up
NODE_PATH="$BREW_PREFIX/lib/node_modules"; export NODE_PATH

# Add custom man pages to PATH
MANPATH="$BREW_PREFIX/share/man:$MANPATH"; export MANPATH

# Set RBENV_ROOT
RBENV_ROOT="$BREW_PREFIX/var/rbenv"; export RBENV_ROOT

#######################################
# Topics
#######################################

function() {
  # all of our zsh files
  typeset -U config_files
  local config_files=($HOME/.dotfiles/topics/**/*.zsh)

  # Load everything but the completion files
  for file in ${config_files:#*/completion.zsh}; do
    source $file
  done

  # Completions
  fpath=($BREW_PREFIX/share/zsh-completions $fpath)

  # Initialize autocompletion
  autoload -U compinit
  compinit

  # Load every completion after autocomplete loads
  for file in ${(M)config_files:#*/completion.zsh}; do
    source $file
  done
}

#######################################
# FASD
#######################################

function () {
  if (( $+commands[fasd] )); then
    local fasd_cache="$HOME/.fasd-init"
    if [ "$(command -v fasd)" -nt "$fasd_cache" -o ! -s "$fasd_cache" ]; then
      fasd --init auto >| "$fasd_cache"
    fi
    source "$fasd_cache"
  fi
}

#######################################
# Sources
#######################################

function () {
  # iTerm 2
  test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

  # brew install zsh-syntax-highlighting
  test -e "/usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh" && source /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

  # NVM
  export NVM_DIR="/Users/davidrapson/.nvm"
  [ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"
}
